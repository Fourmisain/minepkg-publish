plugins {
	id 'groovy-gradle-plugin'
	id 'maven-publish'
}

repositories {
	mavenCentral()
}

group = 'io.github.fourmisain'
version = '0.8.0'

gradlePlugin {
	plugins {
		minepkg_publish {
			id = 'minepkg-publish'
			implementationClass = 'io.github.fourmisain.minepkg.MinepkgPlugin'
		}
	}
}

// Groovy 4 is compatible with Java 8
java {
	targetCompatibility = 8
}

def GITLAB_PRIVATE_ACCESS_TOKEN = System.getenv("GITLAB_PRIVATE_ACCESS_TOKEN")
tasks.register('checkAccessToken') {
	doLast {
		if (GITLAB_PRIVATE_ACCESS_TOKEN == null) {
			throw new GradleException("GITLAB_PRIVATE_ACCESS_TOKEN not defined")
		}
	}
}

publishing {
	repositories {
		maven {
			url = "https://gitlab.com/api/v4/projects/37712942/packages/maven"
			credentials(HttpHeaderCredentials) {
				name = 'Private-Token'
				value = GITLAB_PRIVATE_ACCESS_TOKEN
			}
			authentication {
				header(HttpHeaderAuthentication)
			}
		}
	}
}

tasks.withType(PublishToMavenRepository).configureEach { dependsOn('checkAccessToken') }
